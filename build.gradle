plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'application'
    id 'idea'
    id 'org.openapi.generator' version '5.0.0'
}

group = 'com.sygmatech.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '15'
targetCompatibility = '15'

tasks.withType(JavaCompile) {
    options.compilerArgs += '--enable-preview'
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext['rest-assured.version'] = '4.3.3'
ext["groovy.version"] = "3.0.3"

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.github.resilience4j:resilience4j-spring-boot2:1.5.0'
    implementation 'io.github.resilience4j:resilience4j-circuitbreaker:1.5.0'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.flywaydb:flyway-core'

    implementation 'io.swagger.core.v3:swagger-annotations:2.0.0'
    implementation 'org.threeten:threetenbp:1.3.5'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'javax.validation:validation-api'

    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.8.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:spring-mock-mvc:4.3.3'
}

test {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = "spring"
    generateModelDocumentation = false
    generateApiDocumentation = false
    inputSpec = "$rootDir/banking/openapi.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    invokerPackage = "com.acme.banking.invoker"
    modelPackage = "com.acme.banking.model"
    apiPackage = "com.acme.banking.api"
    configOptions = [
            dateLibrary: "java8",
            basePackage: "com.acme.banking",
            interfaceOnly: "true",
            useBeanValidation: "false"
    ]
}